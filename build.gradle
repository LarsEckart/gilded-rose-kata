plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.19.0-rc.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.json:json:20251224")

    testImplementation(platform("org.junit:junit-bom:6.0.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation("org.assertj:assertj-core:3.27.6")
    testImplementation("com.approvaltests:approvaltests:26.5.0")
    testImplementation("com.larseckart:junit-tcr-extensions:2.0.0")
    testImplementation("org.mockito:mockito-core:5.21.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.21.0")
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.incremental = true
}

test {
    useJUnitPlatform()
    failOnNoDiscoveredTests = false
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

project.tasks.check.dependsOn("pitest")
pitest {
    junit5PluginVersion = '1.2.1'
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    targetClasses = ['com.gildedrose.*']
    pitestVersion = '1.18.0'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
